plugins {
	id "application"
	id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
	id "org.beryx.runtime" version "$badassRuntimePluginVersion"
}

kotlin {
	jvmToolchain {
		languageVersion = JavaLanguageVersion.of(javaVersion)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "io.github.electronstudio:jaylib-ffm:$jaylibVersion"
	implementation "org.xerial:sqlite-jdbc:$sqliteJdbcVersion"

	testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
	testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
	useJUnitPlatform()
}

application {
	mainClass = "dev.capybaralabs.igj2025.LauncherKt"
	applicationDefaultJvmArgs = ["-XstartOnFirstThread"]
}

run {
	if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
		jvmArgs += "-XstartOnFirstThread"
	}
}

runtime {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

	modules = ['java.desktop', 'java.sql', 'jdk.unsupported']

	jpackage {
		imageName = 'igj2025'
		appVersion = '1.0.0'

		// Platform-specific options
		def os = org.gradle.internal.os.OperatingSystem.current()

		if (os.isMacOsX()) {
			jvmArgs = ['-XstartOnFirstThread']
			def macIcon = file('src/main/resources/assets/icon.icns')
			if (macIcon.exists()) {
				imageOptions = ['--icon', macIcon.absolutePath]
			}
		} else if (os.isWindows()) {
			def winIcon = file('src/main/resources/assets/icon.ico')
			if (winIcon.exists()) {
				imageOptions = ['--icon', winIcon.absolutePath]
			}
		} else if (os.isLinux()) {
			def linuxIcon = file('src/main/resources/assets/icon.png')
			if (linuxIcon.exists()) {
				imageOptions = ['--icon', linuxIcon.absolutePath]
			}
		}

		// Resource directory to include assets
		resourceDir = file('src/main/resources')
	}
}
